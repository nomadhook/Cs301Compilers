/*	EBNF using recursion.
	Marq Botha - 14B7599; Damon Hook - 14H0114; Megan Kingon - 14K0129; Kyle Wallace - 12W4298 */

COMPILER EBNFR $CN

CHARACTERS
  l   = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz" .
  d   = "0123456789" .

TOKENS
  letter = l .
  digit  = d .
  
/*COMMENTS FROM "(*" TO "*)"  NESTED*/

IGNORE  CHR(9) .. CHR(13)

PRODUCTIONS
   EBNFR      = EOF | Production EBNFR .
   Production = nonterminal "=" Expression "." .
   Expression = Term ( "|" Expression | ) .
   Term       = Factor (Term | ) .
   Factor     =   nonterminal
                | terminal
                | "[" Expression "]"
                | "(" Expression ")"
                | "{" Expression "}" .
				 
  nonterminal	= letter nonterminalR .
  nonterminalR 	= (letter | "_" | digit) nonterminalR |  .
  terminal     	=  ("'" nq1Terminal)| ('"' nq2Terminal ).
  nq1Terminal 	= noQuote ("'" | nq1Terminal) .
  nq2Terminal	= noQuote ('"' | nq2Terminal) .
  
  noQuote      	= letter | digit | symbol .
  symbol       	= "`" | "~" | "!" | "@" | "#" | "$" | "%" | "^" | "&"
                  | "*" | "(" | ")" | "_" | "+" | "-" | "=" | "[" | "]"
                  | "{" | "}" | "|" | ":" | ";" | "," | "<"
                  | "." | ">" | "/" | "?" .
END EBNFR.
